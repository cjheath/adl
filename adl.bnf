adl		= object*

object		= ascend* path_name? definition

definition	= type? (block? (assignment ';')? | ';') | reference

ascend		= '.'

type		=  ':' path_name? array_indicator?

path_name	= name ('.' name)* local?

local		= '.'

name		= symbol | integer

symbol		= [_[:alpha]] [_[:alnum]]*

integer		= [1-9] [0-9]*

reference	= ('->' | '=>') path_name (block | ';')

array_indicator = '[]'

block		= '{' object* '}'

assignment	= final_assignment | assignment

final_assignment= '=' value

assignment	= '~=' value

value		= integer | string | regexp | path_name | array

string		= '\'' string_char '\''

string_char	= '\\[befntr]' | '\\[0-7]{3}' | '\\x[0-9A-F]{2}' | '\\u[0-9A-F]{4}' | '[^\\\']'

array		= '[' (value (',' value)? ']'

regexp		= '/' regexp_sequence '/'

regexp_sequence	= regexp_alternate ('|' regexp_alternate)*

regexp_alternate = regexp_atom*

regexp_atom	= ( regexp_char | regexp_group | '[' regexp_class+ ']') ( '*' | '+' | '?' )?

regexp_char	= '\' [0-3] [0-7] [0-7]
		| '\x' [0-9A-F] [0-9A-F]
		| '\u' [0-9A-F] [0-9A-F] [0-9A-F] [0-9A-F]
		| '\' [0befntr\\*+?()|/\[]
		| [^*+?()|/\[]

regexp_class_char = regexp_char | [+*?()/|]

regexp_class	= !']' regexp_class_char '-' !']' regexp_class_char
		| '-'
		| !']' regexp_class_char

regexp_group	= '(' regexp_group_capture? regexp_sequence ')'

regexp_group_capture = '?' '<' [a-zA-Z0-9_]+ '>'
