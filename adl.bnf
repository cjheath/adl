// White space includes space, tab, newline and a comment as '//' followed by any characters to end-of-line

adl             = object*

object          = path_name? definition

definition      = (reference | type? block? post_definition ';' | type? block | ';' | alias_from )

post_definition = array_indicator assignment? | assignment

type            =  ':' path_name?

path_name       = ascend* (name (descend name)*)?

ascend          = '.'
descend         = '.'

name            = symbol | integer

symbol          = [_[:alpha:]] [_[:alnum:]]*

integer         = [1-9] [0-9]*

reference       = ('->' | '=>') path_name (';' | block? tentative_assignment ';' | block)

block           = '{' object* '}'

assignment      = final_assignment | tentative_assignment

final_assignment = '=' value

tentative_assignment = '~=' value

array_indicator = '[]'

alias_from      = '!' path_name

// The value syntax depends on the Variable in the assignment.
value           = atomic_value | array

// If the Variable is a reference, the value is a path_name.
// If the Variable is a Regular Expression the value is a regexp.
// Otherwise, the value is defined by the Syntax of the variable (regexp_match).
// See builtin.adl for the syntax of values of builtin variables.
atomic_value    = regexp | path_name | regexp_match

array           = '[' (atomic_value (',' atomic_value)? ']'

// Above here, white-space is free. Below here, no implicit white-space is allowed.

// The syntax for a Regular Expression is not regular, so it must be built-in.
regexp          = '/' regexp_sequence '/'

regexp_sequence = regexp_alternate ('|' regexp_alternate)*

regexp_alternate = regexp_atom*

regexp_atom     = ( regexp_char | regexp_class | regexp_group) ( '*' | '+' | '?' )?

regexp_char     = '\\s'
                | '\\' [0-3] [0-7] [0-7]
                | '\\x' [0-9A-F] [0-9A-F]
                | '\\u' [0-9A-F] [0-9A-F] [0-9A-F] [0-9A-F]
                | '\\' [0befntr\\*+?()|/\[]
                | [^*+?()|/\[ ]

regexp_class    = '[' '^'? '-'? regexp_class_part+ ']'

regexp_class_part = !']' regexp_class_char '-' !']' regexp_class_char
                | !']' regexp_class_char
                | '[:' ('alpha'|'upper'|'lower'|'digit'|'alnum') ':]'

regexp_class_char = !'-' regexp_char | [+*?()/|]

regexp_group    = '(' regexp_group_type? regexp_sequence ')'

regexp_group_type = regexp_group_capture | regexp_negative_lookahead

regexp_negative_lookahead '?!'

regexp_group_capture = '?<' [_[:alnum:]]+ '>'
