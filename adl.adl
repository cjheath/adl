// The ADL object is the namespace for the ADL built-in objects.
// There are a few really built-in ones, but we extend them here.
ADL {
	Object {
		Parent -> Object;		// The container of this object
		Name: String;			// The name of this object
		Super -> Object;		// The supertype of this Object
		Aspect -> Object;		// The aspect to which this object has restricted visibility
		Is Sterile -> Boolean ~= False;	// No further subtypes may be added
		Is Complete -> Boolean ~= False;// No further contents may be added
		Syntax: Regular Expression;	// A literal value conforms to this regular grammar
		Is Array -> Boolean ~= False;	// The assignment is an array of values
	}

	Reference: {Reference = Object};	// The built-in Syntax accepts a path name
	Regular Expression:;			// The built-in Syntax is not regular
	Assignment: {
		Variable -> Object;		// The Object whose Syntax the Value conforms to
		Value: String[];		// The Value(s) must match the Syntax for the Object
		Is Final -> Boolean;		// May not be overridden (but refinement is allowed)
	}

	Enumeration:;				// This is a mnemonic convenience only
	Boolean: Enumeration;			// A Reference to Boolean can only be refined
	False: Boolean;				// To one of the subtypes
	True: Boolean;
	Boolean{Is Sterile = True;}		// and no other alternatives can be added
	Alias: {
		For -> Object;
	}

	Number: {
		Minimum : .;
		Maximum : .;
	};
	Integer: Number {
		Syntax = /[-+]?[1-9][0-9]*/;
	}
	Decimal: Number {
		Syntax = /[-+]?(0|[1-9][0-9]*)(\.[0-9]*)?/;
		Digits: Integer;
		Precision: Integer;
	}
	Real: Decimal {
		Syntax = /[-+]?(0|[1-9][0-9]*)(\.[0-9]*)?([Ee][-+]?[0-9]+)?/;
	}
	Float: Real {
		Exponent: Integer ~= 11;
		Mantissa: Integer ~= 53;
	}

	String: {
		// This is internally defined and cannot be re-assigned
		// Syntax = /'(\\[befntr']|\\[0-7][0-7][0-7]|\\0|\\x[0-9A-F][0-9A-F]|\\u[0-9A-F][0-9A-F][0-9A-F][0-9A-F]|[^\\'])*'/;
		Min Length: Integer;
		Max Length: Integer;
	}

	Temporal:;
	Date: Temporal {
		Syntax = /[0-9][0-9][0-9][0-9][-/][0-9][0-9]?[-/][0-9][0-9]?/;
	}
	Time: Temporal {
		Syntax = /[0-9][0-9]:[0-9][0-9]?:[0-9][0-9]?(.[0-9]+)?/;
		Resolution: Integer{Minimum = 0} ~= 6;
	}
	Date Time: Temporal {
		Syntax = /[0-9][0-9][0-9][0-9][-/][0-9][0-9]?[-/][0-9][0-9]?T[0-9][0-9]:[0-9][0-9]?:[0-9][0-9]?(.[0-9]+)?/;
		Resolution: Integer{Minimum = 0} ~= 0;
	}
}
